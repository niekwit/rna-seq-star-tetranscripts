import os
import datetime
from scripts.resources import Resources
from scripts import general_functions as gf
from snakemake.utils import min_version

# set minimum snakemake version
min_version("6.4.1")

# load config file
configfile: "config/config.yaml"

# load resources to be used in rules
resources = Resources(config["genome"], config["ensembl_genome_build"])

# get sample names
SAMPLES = gf.import_samples()

# set STAR parameters
ofmn = config["mapping"]["outFilterMultimapNmax"]
wamn = config["mapping"]["winAnchorMultimapNmax"]
extra_params = config["mapping"]["extra_params"]
star_extra = f"--outFilterMultimapNmax {ofmn} --winAnchorMultimapNmax {wamn} {extra_params}" 

# import rules
include: "rules/fastqc.smk"
include: "rules/trimming.smk"
include: "rules/resources.smk"
include: "rules/mapping.smk"
include: "rules/te_quantification.smk"
include: "rules/deseq2.smk"
include: "rules/plotting.smk"
include: "rules/bigwig.smk"

# target rule
rule all:
    input: 
        "results/plots/mapping_rates.pdf",
        f"{resources.fasta}.gz",
        f"{resources.gtf}.gz",
        f"{resources.tegtf}.gz",
        "results/plots/sample_distance.pdf",
        "results/plots/volcano_genes/",
        "results/plots/volcano_te/",
        "results/plots/te_classes",
        expand("results/bigwig/{sample}.bw", sample=SAMPLES),

# save snakemake terminal output to log file
time = datetime.datetime.now().strftime("%Y-%m-%d_%H-%M-%S")
snake_log = f"logs/snakemake/{time}_snakemake.log"
os.makedirs("logs/snakemake", exist_ok=True)

onsuccess: 
    shell("cp -v {log} {snake_log}")
    print("Analysis finished successfully!")

onerror:
    shell("cp -v {log} {snake_log}")
    print(f"Analysis (partly) failed...\nCheck {snake_log} for details")



